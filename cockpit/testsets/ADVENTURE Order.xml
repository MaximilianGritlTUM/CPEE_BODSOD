<?xml version="1.0"?>
<testset>
  <positions/>
  <handlerwrapper>DefaultHandlerWrapper</handlerwrapper>
  <dataelements><!--{{{-->
    <test></test>
  </dataelements><!--}}}-->
  <handlers><!--{{{-->
    <handler url="http://localhost:9299/events">
      <events topic='properties/state'>change</events>
    </handler>
  </handlers><!--}}}-->
  <endpoints><!--{{{-->
    <gateway>http://localhost/gateways/timeout.php</gateway>
  </endpoints>  <!--}}}-->
  <description xmlns="http://cpee.org/ns/description/1.0"><!--{{{-->
    <parallel>
      <parallel_branch>
        <call id="a9" endpoint="gateway">
          <parameters><!--{{{-->
            <label>Produce Headcap Subsystem</label>
            <method>post</method>
            <parameters>
              <timeout>4</timeout>
            </parameters>
          </parameters><!--}}}-->
        </call>
        <call id="a10" endpoint="gateway">
          <parameters><!--{{{-->
            <label>Delivery</label>
            <method>post</method>
            <parameters>
              <timeout>4</timeout>
            </parameters>
          </parameters><!--}}}-->
        </call>
      </parallel_branch>
      <parallel_branch>
        <call id="a5" endpoint="gateway">
          <parameters><!--{{{-->
            <label>Supply Mechanical Parts</label>
            <method>post</method>
            <parameters>
              <timeout>4</timeout>
            </parameters>
          </parameters><!--}}}-->
        </call>
        <call id="a6" endpoint="gateway">
          <parameters><!--{{{-->
            <label>Produce Mechanical Parts (A-Z)</label>
            <method>post</method>
            <parameters>
              <timeout>4</timeout>
            </parameters>
          </parameters><!--}}}-->
        </call>
        <call id="a7" endpoint="gateway">
          <parameters><!--{{{-->
            <label>Perform Surface Treatment</label>
            <method>post</method>
            <parameters>
              <timeout>4</timeout>
            </parameters>
          </parameters><!--}}}-->
        </call>
        <call id="a8" endpoint="gateway">
          <parameters><!--{{{-->
            <label>Mechanical Parts Assembly</label>
            <method>post</method>
            <parameters>
              <timeout>4</timeout>
            </parameters>
          </parameters><!--}}}-->
        </call>
      </parallel_branch>
      <parallel_branch>
        <parallel>
          <parallel_branch>
            <call id="a2" endpoint="gateway">
              <parameters><!--{{{-->
                <label>Supply Electric Parts</label>
                <method>post</method>
                <parameters>
                  <timeout>4</timeout>
                </parameters>
              </parameters><!--}}}-->
            </call>
            <call id="a3" endpoint="gateway">
              <parameters><!--{{{-->
                <label>Delivery</label>
                <method>post</method>
                <parameters>
                  <timeout>4</timeout>
                </parameters>
              </parameters><!--}}}-->
            </call>
          </parallel_branch>
          <parallel_branch>
            <call id="a1" endpoint="gateway">
              <parameters><!--{{{-->
                <label>Develop Control System</label>
                <method>post</method>
                <parameters>
                  <timeout>4</timeout>
                </parameters>
              </parameters><!--}}}-->
            </call>
          </parallel_branch>
        </parallel>
        <call id="a4" endpoint="gateway">
          <parameters><!--{{{-->
            <label>Electric Board Assembly</label>
            <method>post</method>
            <parameters>
              <timeout>4</timeout>
            </parameters>
          </parameters><!--}}}-->
        </call>
      </parallel_branch>
    </parallel>  
    <call id="a11" endpoint="gateway">
      <parameters><!--{{{-->
        <label>Systems Integration</label>
        <method>post</method>
        <parameters>
          <timeout>4</timeout>
        </parameters>
      </parameters><!--}}}-->
    </call>
    <call id="a12" endpoint="gateway">
      <parameters><!--{{{-->
        <label>Test</label>
        <method>post</method>
        <parameters>
          <timeout>4</timeout>
        </parameters>
      </parameters><!--}}}-->
      <manipulate output="result">  data.test = "success"</manipulate>
    </call>
  </description><!--}}}-->
  <transformation><!--{{{-->
    <xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:d="http://cpee.org/ns/description/1.0">
      <xsl:output method="text"/>
      <xsl:strip-space elements="*"/>
      <xsl:variable name="myspacemultiplier">2</xsl:variable>

      <xsl:template match="/">
        <xsl:apply-templates select="//d:description"/>
      </xsl:template>

      <xsl:template match="//d:description">
        <xsl:apply-templates>
          <xsl:with-param name="myspace"><xsl:value-of select="-1*$myspacemultiplier"/></xsl:with-param>
        </xsl:apply-templates>  
      </xsl:template>

      <xsl:template match="*">
        <xsl:param name="myspace"/>
        <xsl:call-template name="print-space">
          <xsl:with-param name="i">1</xsl:with-param>
          <xsl:with-param name="count"><xsl:value-of select="$myspace+$myspacemultiplier"/></xsl:with-param>
        </xsl:call-template>
        <xsl:if test="name()='call' or name()='manipulate'">
          <xsl:text>activity :</xsl:text>
          <xsl:value-of select="@id"/>
        </xsl:if>
        <xsl:if test="name()='call'">
          <xsl:text>, :call, :</xsl:text>
          <xsl:value-of select="@endpoint"/>
          <xsl:apply-templates select="d:parameters"/>
          <xsl:apply-templates select="d:manipulate" mode="part-of-call">
            <xsl:with-param name="myspace"><xsl:value-of select="$myspace"/></xsl:with-param>
          </xsl:apply-templates>
          <xsl:call-template name="print-newline"/>
        </xsl:if>
        <xsl:if test="name()='manipulate'">
          <xsl:text>, :manipulate</xsl:text>
          <xsl:call-template name="print-content">
            <xsl:with-param name="myspace"><xsl:value-of select="$myspace"/></xsl:with-param>
          </xsl:call-template>
          <xsl:call-template name="print-newline"/>
        </xsl:if>
        <xsl:if test="name()='parallel'">
          <xsl:text>parallel</xsl:text>
          <xsl:if test="@wait">
            <xsl:text> :wait => </xsl:text>
            <xsl:value-of select="@wait"/>
          </xsl:if>
          <xsl:text> do</xsl:text>
          <xsl:call-template name="print-newline"/>
          <xsl:apply-templates>
            <xsl:with-param name="myspace"><xsl:value-of select="$myspace+$myspacemultiplier"/></xsl:with-param>
          </xsl:apply-templates>
          <xsl:call-template name="print-space">
            <xsl:with-param name="i">1</xsl:with-param>
            <xsl:with-param name="count"><xsl:value-of select="$myspace+$myspacemultiplier"/></xsl:with-param>
          </xsl:call-template>
          <xsl:text>end</xsl:text>
          <xsl:call-template name="print-newline"/>
        </xsl:if>
        <xsl:if test="name()='loop'">
          <xsl:text>loop </xsl:text>
          <xsl:if test="@pre_test">
            <xsl:text>pre_test{</xsl:text>
            <xsl:value-of select="@pre_test"/>
            <xsl:text>} </xsl:text>
          </xsl:if>
          <xsl:if test="@post_test">
            <xsl:text>post_test{</xsl:text>
            <xsl:value-of select="@post_test"/>
            <xsl:text>} </xsl:text>
          </xsl:if>
          <xsl:text>do</xsl:text>
          <xsl:call-template name="print-newline"/>
          <xsl:apply-templates>
            <xsl:with-param name="myspace"><xsl:value-of select="$myspace+$myspacemultiplier"/></xsl:with-param>
          </xsl:apply-templates>
          <xsl:call-template name="print-space">
            <xsl:with-param name="i">1</xsl:with-param>
            <xsl:with-param name="count"><xsl:value-of select="$myspace+$myspacemultiplier"/></xsl:with-param>
          </xsl:call-template>
          <xsl:text>end</xsl:text>
          <xsl:call-template name="print-newline"/>
        </xsl:if>
        <xsl:if test="name()='choose'">
          <xsl:text>choose do</xsl:text>
          <xsl:call-template name="print-newline"/>
          <xsl:apply-templates>
            <xsl:with-param name="myspace"><xsl:value-of select="$myspace+$myspacemultiplier"/></xsl:with-param>
          </xsl:apply-templates>
          <xsl:call-template name="print-space">
            <xsl:with-param name="i">1</xsl:with-param>
            <xsl:with-param name="count"><xsl:value-of select="$myspace+$myspacemultiplier"/></xsl:with-param>
          </xsl:call-template>
          <xsl:text>end</xsl:text>
          <xsl:call-template name="print-newline"/>
        </xsl:if>
        <xsl:if test="name()='group'">
          <xsl:apply-templates>
            <xsl:with-param name="myspace"><xsl:value-of select="$myspace+$myspacemultiplier"/></xsl:with-param>
          </xsl:apply-templates>
          <xsl:call-template name="print-newline"/>
        </xsl:if>
        <xsl:if test="name()='critical'">
          <xsl:text>critical :</xsl:text>
          <xsl:value-of select="@sid"/>
          <xsl:text> do</xsl:text>
          <xsl:call-template name="print-newline"/>
          <xsl:apply-templates>
            <xsl:with-param name="myspace"><xsl:value-of select="$myspace+$myspacemultiplier"/></xsl:with-param>
          </xsl:apply-templates>
          <xsl:call-template name="print-space">
            <xsl:with-param name="i">1</xsl:with-param>
            <xsl:with-param name="count"><xsl:value-of select="$myspace+$myspacemultiplier"/></xsl:with-param>
          </xsl:call-template>
          <xsl:text>end</xsl:text>
          <xsl:call-template name="print-newline"/>
        </xsl:if>
      </xsl:template>
      
      <xsl:template match="d:alternative">
        <xsl:param name="myspace"/>
        <xsl:call-template name="print-space">
          <xsl:with-param name="i">1</xsl:with-param>
          <xsl:with-param name="count"><xsl:value-of select="$myspace+$myspacemultiplier"/></xsl:with-param>
        </xsl:call-template>
        <xsl:text>alternative </xsl:text>
        <xsl:value-of select="@condition"/>
        <xsl:text> do</xsl:text>
        <xsl:call-template name="print-newline"/>
        <xsl:apply-templates>
          <xsl:with-param name="myspace"><xsl:value-of select="$myspace+$myspacemultiplier"/></xsl:with-param>
        </xsl:apply-templates>
        <xsl:call-template name="print-space">
          <xsl:with-param name="i">1</xsl:with-param>
          <xsl:with-param name="count"><xsl:value-of select="$myspace+$myspacemultiplier"/></xsl:with-param>
        </xsl:call-template>
        <xsl:text>end</xsl:text>
        <xsl:call-template name="print-newline"/>
      </xsl:template>

      <xsl:template match="d:otherwise">
        <xsl:param name="myspace"/>
        <xsl:call-template name="print-space">
          <xsl:with-param name="i">1</xsl:with-param>
          <xsl:with-param name="count"><xsl:value-of select="$myspace+$myspacemultiplier"/></xsl:with-param>
        </xsl:call-template>
        <xsl:text>otherwise do</xsl:text>
        <xsl:call-template name="print-newline"/>
        <xsl:apply-templates>
          <xsl:with-param name="myspace"><xsl:value-of select="$myspace+$myspacemultiplier"/></xsl:with-param>
        </xsl:apply-templates>
        <xsl:call-template name="print-space">
          <xsl:with-param name="i">1</xsl:with-param>
          <xsl:with-param name="count"><xsl:value-of select="$myspace+$myspacemultiplier"/></xsl:with-param>
        </xsl:call-template>
        <xsl:text>end</xsl:text>
        <xsl:call-template name="print-newline"/>
      </xsl:template>

      <xsl:template match="d:parallel_branch">
        <xsl:param name="myspace"/>
        <xsl:call-template name="print-space">
          <xsl:with-param name="i">1</xsl:with-param>
          <xsl:with-param name="count"><xsl:value-of select="$myspace+$myspacemultiplier"/></xsl:with-param>
        </xsl:call-template>
        <xsl:text>parallel_branch</xsl:text>
        <xsl:if test="@pass">
          <xsl:text> </xsl:text>
          <xsl:value-of select="@pass"/>
        </xsl:if>
        <xsl:text> do</xsl:text>
        <xsl:if test="@local">
          <xsl:text> |</xsl:text>
          <xsl:value-of select="@local"/>
          <xsl:text>|</xsl:text>
        </xsl:if>
        <xsl:call-template name="print-newline"/>
        <xsl:apply-templates>
          <xsl:with-param name="myspace"><xsl:value-of select="$myspace+$myspacemultiplier"/></xsl:with-param>
        </xsl:apply-templates>
        <xsl:call-template name="print-space">
          <xsl:with-param name="i">1</xsl:with-param>
          <xsl:with-param name="count"><xsl:value-of select="$myspace+$myspacemultiplier"/></xsl:with-param>
        </xsl:call-template>
        <xsl:text>end</xsl:text>
        <xsl:call-template name="print-newline"/>
      </xsl:template>

      <xsl:template match="d:parameters">
        <xsl:apply-templates select="d:*" mode="parameter"/>
      </xsl:template>
        
      <xsl:template match="d:*" mode="parameter">
        <xsl:text>, :</xsl:text>
        <xsl:value-of select="name()"/>
        <xsl:text> => </xsl:text>
        <xsl:choose>  
          <xsl:when test="count(*) > 0">
            <xsl:text>[</xsl:text>
            <xsl:apply-templates select="d:*" mode="sub-parameter"/>
            <xsl:text>]</xsl:text>
          </xsl:when>
          <xsl:otherwise>
            <xsl:choose>
              <xsl:when test="@type = 'symbolic'">
                <xsl:value-of select="text()"/>
              </xsl:when>
              <xsl:otherwise>
                <xsl:text>"</xsl:text>
                <xsl:value-of select="text()"/>
                <xsl:text>"</xsl:text>
              </xsl:otherwise>
            </xsl:choose>  
          </xsl:otherwise>
        </xsl:choose>  
      </xsl:template>
      
      <xsl:template match="d:*" mode="sub-parameter">
        <xsl:text> { :</xsl:text>
        <xsl:value-of select="name()"/>
        <xsl:text> => </xsl:text>
        <xsl:value-of select="text()"/>
        <xsl:text> }</xsl:text>
        <xsl:choose>  
          <xsl:when test="generate-id(.) = generate-id(../*[last()])">
            <xsl:text> </xsl:text>
          </xsl:when>
          <xsl:otherwise>
            <xsl:text>,</xsl:text>
          </xsl:otherwise>
        </xsl:choose>  
      </xsl:template>

      <xsl:template match="d:manipulate" mode="part-of-call">
        <xsl:param name="myspace"/>
        <xsl:call-template name="print-content">
          <xsl:with-param name="myspace"><xsl:value-of select="$myspace"/></xsl:with-param>
        </xsl:call-template>
      </xsl:template>

      <xsl:template name="print-content">
        <xsl:param name="myspace"/>
        <xsl:if test="text()">
          <xsl:text> do </xsl:text>
          <xsl:if test="@output">
            <xsl:text>|</xsl:text>
            <xsl:value-of select="@output"/>
            <xsl:text>|</xsl:text>
          </xsl:if>
          <xsl:call-template name="print-newline"/>
          <xsl:value-of select="text()"/>
          <xsl:call-template name="print-newline"/>
          <xsl:call-template name="print-space">
            <xsl:with-param name="i">1</xsl:with-param>
            <xsl:with-param name="count"><xsl:value-of select="$myspace+$myspacemultiplier"/></xsl:with-param>
          </xsl:call-template>
          <xsl:text>end</xsl:text>
        </xsl:if>
      </xsl:template>

      <xsl:template name="print-space">
        <xsl:param name="i"/>
        <xsl:param name="count"/>
        <xsl:if test="$i &lt;= $count">
          <xsl:text> </xsl:text>
          <xsl:call-template name="print-space">
            <xsl:with-param name="i">
              <xsl:value-of select="$i + 1"/>
            </xsl:with-param>
            <xsl:with-param name="count">
              <xsl:value-of select="$count"/>
            </xsl:with-param>
          </xsl:call-template>
        </xsl:if>
      </xsl:template>
      <xsl:template name="print-newline">
        <xsl:text>
</xsl:text>
      </xsl:template>

    </xsl:stylesheet>
  </transformation><!--}}}-->
</testset>
